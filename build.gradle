plugins {
    id 'java' apply true
    id "io.freefair.lombok" version "8.6" apply true
    id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
    id "com.vanniktech.maven.publish" version "0.19.0" apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'io.freefair.lombok'

    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublish {
            sonatypeHost = "S01"
        }
    }

    configurations {
        runtimeClasspath {
            exclude group: 'com.squareup.okhttp3', module: 'okhttp'
            exclude group: 'com.squareup.okio', module: 'okio'
        }
    }

    shadowJar {
        configurations = [project.configurations.runtimeClasspath]
        archiveBaseName.set('openai4j+' + project.name)
        archiveVersion.set(VERSION_NAME)
        archiveClassifier.set('fat')
        mergeServiceFiles()
        manifest {
            attributes 'Encoding': 'UTF-8'
        }
    }

    compileJava.options.encoding = 'UTF-8'
}

configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'com.fasterxml.jackson.core') {
            details.useVersion libs.getVersions().jackson as String
        }
    }
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]
    from subprojects.collect { it.shadowJar.outputs.files }
    archiveBaseName.set('openai4j')
    archiveVersion.set(VERSION_NAME)
    archiveClassifier.set('fat')
}